plugins {
    id "java"
}

group = "com.saucelabs.test"
version = "1.0.0"

jar {
    archiveBaseName = "saucelabs-selenium-junit5"
}

compileTestJava {
    sourceCompatibility = 11
    targetCompatibility = 11
    options.compilerArgs += "-parameters"
}

test {
    useJUnitPlatform()

    testLogging {
        events "passed", "skipped", "failed"
        showStandardStreams = true
    }

    systemProperties System.properties
}

repositories {
    mavenCentral()
    mavenLocal()
}

dependencies {
    testImplementation("com.saucelabs.test:saucelabs-selenium-diy:1.0.0")

    testImplementation("org.seleniumhq.selenium:selenium-java:4.5.3")
    testImplementation("org.junit.jupiter:junit-jupiter:5.9.1")
    testImplementation("org.junit.platform:junit-platform-launcher:1.9.1")
    testImplementation("org.assertj:assertj-core:3.23.1")

    testImplementation("org.slf4j:slf4j-api:2.0.3")
    testImplementation("ch.qos.logback:logback-classic:1.4.4")
}


task fatJar(type: Jar) {
    manifest {
        attributes( 
                "Main-Class": "com.saucelabs.test.RunSuiteMain"
        )
    }
    classifier = "fat"
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    from { 
        sourceSets.test.output
    }
    from { 
        configurations.testRuntimeClasspath.collect { it.isDirectory() ? it : zipTree(it) } 
    } {
        exclude "META-INF/*.SF"
        exclude "META-INF/*.DSA"
        exclude "META-INF/*.RSA"
    }
    with jar
}
